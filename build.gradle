plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.github.can019'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

tasks.withType(Test){
	useJUnitPlatform()
	maxParallelForks = Runtime.runtime.availableProcessors()
}

sourceSets {
	integrationTest {
		java.srcDir "$projectDir/src/integration/java"
		resources.srcDir "$projectDir/src/integration/resources"
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}

	unitTest {
		java.srcDir "$projectDir/src/unit/java"
		resources.srcDir "$projectDir/src/unit/resources"
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integrationTestImplementation.extendsFrom implementation
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly

	unitTestImplementation.extendsFrom implementation
	unitTestImplementation.extendsFrom testImplementation
	unitTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	implementation 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.testcontainers:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	runtimeOnly 'com.h2database:h2'
	// uuid-v1
	implementation 'com.fasterxml.uuid:java-uuid-generator:5.0.0'

	testImplementation "org.testcontainers:mysql:1.19.8"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	runtimeOnly 'com.mysql:mysql-connector-j'
	testImplementation "org.testcontainers:mysql"
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'


	// Logback을 log4j2로 대체
	implementation("org.springframework.boot:spring-boot-starter-log4j2")
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
	modules {
		module("org.springframework.boot:spring-boot-starter-logging") {
			replacedBy("org.springframework.boot:spring-boot-starter-log4j2")
		}
	}
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

task unitTest(type: Test) {
	testClassesDirs = sourceSets.unitTest.output.classesDirs
	classpath = sourceSets.unitTest.runtimeClasspath
}

check.dependsOn integrationTest
check.dependsOn unitTest